apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

repositories {
    mavenCentral()
    maven { url 'http://guardian.github.com/maven/repo-releases' }
}

dependencies {
    compile "com.android.support:appcompat-v7:26.1.0"
    compile "com.android.support:support-annotations:26.1.0"
    compile 'com.gu:option:1.3'
    compile 'com.github.frankiesardo:auto-parcel:0.3.1'
    compileOnly "com.google.auto.value:auto-value:1.5"
    annotationProcessor "com.google.auto.value:auto-value:1.5"
    compile 'com.github.frankiesardo:auto-parcel-processor:0.3.1'
    annotationProcessor 'com.github.frankiesardo:auto-parcel-processor:0.3.1'
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

checkstyle {
    configFile project.file('../config/checkstyle/checkstyle.xml')
    showViolations false
}


gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign } &&
            ':library:uploadArchives' in gradle.startParameter.taskNames) {
        // Use Java 6's console to read from the console (no good for a CI environment)
        Console console = System.console()
        console.printf "\n\nWe have to sign some things in this build.\n\nPlease enter your signing details.\n\n"
        allprojects { ext."signing.password" = console.readPassword("PGP Private Key Password: ") }
    }
}
